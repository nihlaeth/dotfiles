# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/.dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files - skip zshplugins
for file in ${${${(M)config_files:#*/path.zsh}:#*/zshplugins/*}:#*/hg-plugins/*}
do
  source $file
done

# load everything but the path and completion files - skip zshplugins!
for file in ${${${${config_files:#*/path.zsh}:#*/completion.zsh}:#*/zshplugins/*}:#*/hg-plugins/*}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads - skip zshplugins
for file in ${${${(M)config_files:#*/completion.zsh}:#*/zshplugins/*}:#*/hg-plugins/*}
do
  source $file
done

unset config_files
source $HOME/.dotfiles/zshplugins/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh

# added by travis gem
[ -f /root/.travis/travis.sh ] && source /root/.travis/travis.sh
